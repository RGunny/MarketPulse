plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.3' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
    id 'com.github.spotbugs' version "6.2.3"
}

// 버전 관리
ext {
    versions = [
        // gRPC
        grpc: '1.58.0',
        grpcSpringBoot: '2.15.0.RELEASE',
        protobuf: '3.24.4',
        
        // Test
        testcontainers: '1.19.0',
        mockwebserver: '4.12.0',
        
        // Others
        javaxAnnotation: '1.3.2'
    ]
}

// 모든 프로젝트에 공통 설정 적용
allprojects {
    group = 'me.rgunny.marketpulse'
    version = '1.0.0'

    repositories {
        mavenCentral()
    }
}

// 서브 프로젝트에만 적용되는 설정
subprojects {
    apply plugin: 'java'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    // common 모듈은 라이브러리로 설정
    if (project.name == 'common') {
        apply plugin: 'java-library'
        apply plugin: 'io.spring.dependency-management'

        jar {
            enabled = true
            archiveClassifier = ''
        }

    } else {
        // 비즈니스 모듈들은 Spring Boot 적용
        apply plugin: 'org.springframework.boot'
        apply plugin: 'io.spring.dependency-management'
    }

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:3.5.3")
            mavenBom("org.junit:junit-bom:5.10.0")
            mavenBom("org.testcontainers:testcontainers-bom:${versions.testcontainers}")
        }
    }

    // 모든 서브 프로젝트 공통 의존성
    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        
        // 테스트
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
        
        // Testcontainers 공통
        testImplementation 'org.testcontainers:junit-jupiter'
        testImplementation 'org.testcontainers:testcontainers'
    }

    // 테스트 설정
    tasks.test {
        useJUnitPlatform()
    }
}